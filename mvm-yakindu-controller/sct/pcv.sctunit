testclass pcv for statechart MVMStateMachine { 

    // normal operation at a regular rate
	@Test
	operation testNormalOperation () {
		inspiration_duration_ms = 1000
		expiration_duration_ms = 1200
		max_rm_time = 3000
		mock dropPAW_ITS_PCV returns (false)
		enter
		assert active (main_region.StartUp)
		raise startupEnded
		proceed 1 cycle
		assert active (main_region.PCV.r1.VentilationOff)
		raise startVentilation
		proceed 1 cycle
		assert active (main_region.PCV.r1.Inspiration)
		proceed 1000 ms
		assert active (main_region.PCV.r1.Expiration)
		proceed 1200 ms
		assert active (main_region.PCV.r1.Inspiration)
		proceed 500 ms
		assert active (main_region.PCV.r1.Inspiration)
		proceed 500 ms
		assert active (main_region.PCV.r1.Expiration)
		proceed 1 cycle
		assert active (main_region.PCV.r1.Expiration)
		proceed 1200 ms
		assert active (main_region.PCV.r1.Inspiration)
		exit
	} 
	// stop the machine
	@Test
	operation testStop () {
		inspiration_duration_ms = 1000
		expiration_duration_ms = 2000		
		max_rm_time = 3000
		mock dropPAW_ITS_PCV returns (false)
		enter
		assert active (main_region.StartUp)
		raise startupEnded
		proceed 1 cycle
		assert active (main_region.PCV.r1.VentilationOff)
		raise startVentilation
		proceed 1 cycle
		assert active (main_region.PCV.r1.Inspiration)
		proceed 1000 ms
		assert active (main_region.PCV.r1.Expiration)
		proceed 100 ms
		assert active (main_region.PCV.r1.Expiration) //arrives a stop			
		raise stopVentilation
		proceed 1 cycle
		assert active (main_region.PCV.r1.VentilationOff)
		exit
	}

	@Test
	operation testExpiratoryPause () {
		inspiration_duration_ms = 1000
		expiration_duration_ms = 2000		
		max_rm_time = 3000
		mock dropPAW_ITS_PCV returns (false)
		enter
		assert active (main_region.StartUp)
		raise startupEnded
		proceed 1 cycle
		assert active (main_region.PCV.r1.VentilationOff)
		raise startVentilation
		proceed 1 cycle
		assert active (main_region.PCV.r1.Inspiration)
		proceed 1000 ms
		assert active (main_region.PCV.r1.Expiration)
		proceed 100 ms
		exp_pause = true // pause but the exp must finish
		assert active (main_region.PCV.r1.Expiration)
		proceed 1900 ms // now it can go to pause
		assert active (main_region.PCV.r1.ExpiratoryPause)
		proceed 50 ms
		assert active (main_region.PCV.r1.ExpiratoryPause)
		exp_pause = false // quit pause
		proceed 1 cycle
		assert active (main_region.PCV.r1.Inspiration)
		exit
	}
}
